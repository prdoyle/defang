import static java.util.stream.Collectors.joining;

plugins {
    id 'java'
    id 'me.champeau.jmh' version '0.7.2'
}

group = 'works.bosk'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    defangRuntime
}

dependencies {
    defangRuntime project(":defang-runtime")
    implementation 'org.ow2.asm:asm:9.6'
    implementation 'org.ow2.asm:asm-commons:9.6'
    implementation 'org.ow2.asm:asm-util:9.6'
    implementation 'ch.qos.logback:logback-classic:1.5.7'
    implementation project(":defang-runtime")
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.openjdk.jmh:jmh-core:1.37'
    testAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
}

test {
    dependsOn(jar)
    useJUnitPlatform()
    def runtimeJars = configurations.defangRuntime.files.stream().map(File::toString).collect(joining(File.pathSeparator))
    jvmArgs "-Ddefang.runtimeJars=$runtimeJars"
    jvmArgs "-javaagent:${jar.archiveFile.get().asFile}"
}


jar {
    manifest {
        attributes(
                'Premain-Class': 'works.bosk.defang.agent.Agent'
//                , 'Can-Redefine-Classes': 'true'
                , 'Can-Retransform-Classes': 'true'
        )
    }
}
