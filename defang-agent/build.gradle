import static java.util.stream.Collectors.joining;

plugins {
    id 'java'
}

group = 'works.bosk'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    defangRuntime
}

dependencies {
    defangRuntime project(":defang-runtime")
    implementation 'org.ow2.asm:asm:9.6'
    implementation 'org.ow2.asm:asm-commons:9.6'
    implementation project(":defang-runtime")
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    dependsOn(jar)
    useJUnitPlatform()
//	jvmArgs "--patch-module java.base=${jar.archiveFile.get().asFile}"
    def runtimeJars = configurations.defangRuntime.files.stream().map(File::toString).collect(joining(File.pathSeparator))
//    System.out.println("Jars: " + runtimeJars)
    jvmArgs "-Ddefang.runtimeJars=$runtimeJars"
    jvmArgs "-javaagent:${jar.archiveFile.get().asFile}"
}


jar {
    manifest {
        attributes(
                'Premain-Class': 'works.bosk.defang.agent.Agent',
                'Can-Redefine-Classes': 'true',
                'Can-Retransform-Classes': 'true'
        )
    }
}
